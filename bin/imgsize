#!/usr/bin/perl -- # --*-Perl-*--

use strict;
use English;
use Getopt::Std;
use Image::Info qw(image_info);

my $usage = "Usage: $0 [dir]\n";

my $dir = shift @ARGV || ".";
chdir ($dir) || die "Cannot chdir $dir\n";

if (! -d "500") {
    foreach my $size ("64", "150", "500", "1024", "ORIG") {
        mkdir($size);
    }
    system("touch 64/.s");
    system("touch 150/.h");
}

my @sizes = ();
opendir (DIR, ".");
while (my $name = readdir(DIR)) {
    next unless $name =~ /^\d+$/;
    push (@sizes, int($name));
}
closedir (DIR);

die $usage if !@sizes;

opendir (DIR, ".");
while (my $name = readdir(DIR)) {
    next if -d $name;
    next if $name !~ /\.jpg$/;
    my $base = $name;
    $base =~ s/^(.*)\.[^\.]+$/\1/;

    my $info = image_info($name);
    my $width = $info->{'width'};
    my $height = $info->{'height'};

    foreach my $size (@sizes) {
        my $maxwidth = $size;
        my $maxheight = $size;
        my $crop = "";

        # do height first because sometimes that's all we care about...
        my $hscale = $maxheight / $height;
        my $newwidth = int($width * $hscale);
        my $newheight = int($height * $hscale);

        if ($width < $size && $height < $size) {
            $newheight = $height;
            $newwidth = $width;
        }

        if (! -f "$size/.h" && ($newwidth > $maxwidth)) {
            my $wscale = $maxwidth / $width;
            $newwidth = int($width * $wscale);
            $newheight = int($height * $wscale);
        }

        if (-f "$size/.s" && $width != $height) { # Crop to square
            if ($width > $height) {
                $newwidth = $height;
                $newheight = $height;
                $crop = int(($width - $height) / 2);
                $crop = "-crop ${newwidth}x${newheight}+${crop}+0";
            } else {
                $newwidth = $width;
                $newheight = $width;
                $crop = int(($height - $width) / 2);
                $crop = "-crop ${newwidth}x${newheight}+0+${crop}";
            }
            $newwidth = $size;
            $newheight = $size;
        }

        if (-d "xml" && ! -f "xml/$base.xml") {
            print "$name => xml metadata\n";
            system("exiftool -X $name > xml/$base.xml");
        }

        if (! -f "$size/$name") {
            print "$name ($width,$height) => ($newwidth,$newheight)\n";
            system "convert $crop -geometry ${newwidth}x${newheight} \"$name\" \"$size/$name\"";
        }
    }

    # Now cleanup the original
    if (! -f "ORIG/$name") {
        system ("mv \"$name\" ORIG/") && die "Failed to move original: $name\n";
        if (-f "1024/$name") {
            system ("cp \"1024/$name\" .") && die "Failed to move 1024 to current\n";
        }
    }
}
